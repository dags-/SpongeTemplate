plugins {
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id "java"
}

group "me.dags"
version "0.1-SNAPSHOT"

def spongeAPI = "7.1.0"
def spongeChannel = "SNAPSHOT"
def libraries = "${project.group}.${project.name.toLowerCase()}.libs"

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "http://repo.spongepowered.org/maven" }
}

dependencies {
    compileOnly "org.spongepowered:spongeapi:${spongeAPI}-$spongeChannel"
    shade "com.github.dags-:CommandBus:2.0.3"
    shade "com.github.dags-:TextMU:0.1.1"
    shade ("com.github.dags-:Config:1.0.2") {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate "me.dags.textmu.", "${libraries}.text."
    relocate "me.dags.config.", "${libraries}.config."
    relocate "me.dags.commandbus.", "${libraries}.command."
    archiveName = "${baseName}-${version}-SpongeAPI-${spongeAPI}.jar"
}

build.dependsOn shadowJar
jar.enabled = false